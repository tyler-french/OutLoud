load("@bazel_gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@pypi//:requirements.bzl", "all_whl_requirements")
load("@rules_python//python:defs.bzl", "py_binary", "py_library", "py_test")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")
load("@rules_python_gazelle_plugin//manifest:defs.bzl", "gazelle_python_manifest")
load("@rules_python_gazelle_plugin//modules_mapping:def.bzl", "modules_mapping")

package(default_visibility = ["//visibility:public"])

exports_files(["requirements.lock"])

compile_pip_requirements(
    name = "requirements",
    timeout = "long",
    src = "requirements.txt",
    extra_args = [
        "--allow-unsafe",
        "--resolver=backtracking",
    ],
    requirements_txt = "requirements.lock",
    tags = ["manual"],
)

gazelle_binary(
    name = "gazelle_binary",
    tags = ["manual"],
    languages = ["@rules_python_gazelle_plugin//python"],
)

modules_mapping(
    name = "modules_map",
    tags = ["manual"],
    wheels = all_whl_requirements,
)

gazelle_python_manifest(
    name = "gazelle_python_manifest",
    modules_mapping = "//:modules_map",
    pip_repository_name = "pypi",
    requirements = "//:requirements.lock",
    tags = ["manual"],
)

# gazelle:python_root
# gazelle:exclude e2e_test.py
# gazelle:resolve py python.runfiles @rules_python//python/runfiles
# gazelle:resolve py python.runfiles.runfiles @rules_python//python/runfiles
gazelle(
    name = "gazelle",
    tags = ["manual"],
    gazelle = ":gazelle_binary",
)

filegroup(
    name = "espeak_data",
    srcs = select({
        "@platforms//os:linux": ["@espeak_ng//:files"],
        "//conditions:default": [],
    }),
)

py_binary(
    name = "app",
    srcs = ["app.py"],
    data = [
        ":espeak_data",
        ":model_files",
        ":static_files",
        ":templates",
    ],
    visibility = ["//:__subpackages__"],
    deps = [
        "//outloud/config",
        "//outloud/db",
        "//outloud/tts",
        "//outloud/worker",
        "@pypi//flask",
        "@pypi//werkzeug",
    ],
)

py_binary(
    name = "cli",
    srcs = ["cli.py"],
    data = [
        ":espeak_data",
        ":model_files",
    ],
    visibility = ["//:__subpackages__"],
    deps = [
        "//outloud/cleaner",
        "//outloud/extractor",
        "//outloud/tts",
    ],
)

alias(
    name = "ruff",
    actual = select({
        "@platforms//os:linux": "@ruff_linux_x86_64//:ruff",
        "@platforms//os:macos": "@ruff_macos_aarch64//:ruff",
    }),
)

filegroup(
    name = "templates",
    srcs = glob(["templates/**"]),
)

filegroup(
    name = "static_files",
    srcs = glob(["static/**"]),
)

filegroup(
    name = "model_files",
    srcs = [
        "@kokoro_model//file",
        "@kokoro_voices//file",
    ],
)

py_test(
    name = "e2e_test",
    srcs = [
        "app.py",
        "e2e_test.py",
    ],
    args = ["-v"],
    data = [
        "bazel.pdf",
        ":espeak_data",
        ":model_files",
        ":static_files",
        ":templates",
    ],
    main = "e2e_test.py",
    deps = [
        "//outloud/config",
        "//outloud/db",
        "//outloud/tts",
        "//outloud/worker",
        "@pypi//flask",
        "@pypi//pytest",
    ],
)

py_library(
    name = "outloud_lib",
    srcs = [
        "app.py",
        "cli.py",
    ],
    visibility = ["//:__subpackages__"],
    deps = [
        "//outloud/cleaner",
        "//outloud/config",
        "//outloud/db",
        "//outloud/extractor",
        "//outloud/tts",
        "//outloud/worker",
        "@pypi//flask",
        "@pypi//werkzeug",
    ],
)
